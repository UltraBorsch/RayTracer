#pragma kernel TraceRays
#pragma kernel CorrectImage
#pragma kernel AAReduction

#include "Includes/Geometry.cginc" //already includes constants and structs

//global variables that do not change
float4 ambientColour;
float ambientIntensity;

RWTexture2D<float4> result; //raytrace result
float4 resolution; //.x and .y are the resolution, .z and .w are the correspondeg inverses
float samples, sampleInv, nextSampleInv, sampleSquare, sampleSquareInv;
RWStructuredBuffer<float4> AAResults;

//d is (i think) focal length. near/far planes not considered yet
float3 camDirection; //direction TO cam, not direction cam is facing
float3 camPosition;

//variables for generating rays
float d;
float top;
float right;
float bottom;
float left;
float3 u;
float3 v;

RWStructuredBuffer<Light> lights;
int lightCount;
RWStructuredBuffer<Mat> mats;
int matCount;

//scene objects
RWStructuredBuffer<Sphere> spheres;
int sphereCount;

RWStructuredBuffer<Plane> planes;
int planeCount;

RWStructuredBuffer<Quadric> quadrics;
int quadricCount;

RWStructuredBuffer<AABB> boxes;
int AABBCount;

RWStructuredBuffer<Mesh> meshes;
int meshCount;

//mesh buffers
RWStructuredBuffer<float3> vertices;
RWStructuredBuffer<float3> normals;
RWStructuredBuffer<int3> triangles;
int vertexCount, triangleCount;

Intersection CheckForIntersections( RWStructuredBuffer<Mesh> objs, int count, Intersection intersection, Ray ray);
Intersection CheckIntersections (Ray ray, Intersection intersection) {
    intersection = CheckForIntersections(spheres, sphereCount, intersection, ray);
    intersection = CheckForIntersections(planes, planeCount, intersection, ray);
    intersection = CheckForIntersections(quadrics, quadricCount, intersection, ray);
    intersection = CheckForIntersections(boxes, AABBCount, intersection, ray);
    return CheckForIntersections(meshes, meshCount, intersection, ray);
}

//Note, product must be <= 1024
[numthreads(16, 16, 4)]
void TraceRays (uint3 id : SV_DispatchThreadID) {
    if (id.x >= (uint) resolution.x || id.y >= (uint) resolution.y || id.z >= (uint) sampleSquare) return;
    
    //declare/create variables to reduce unnecessary memory allocation in loops
    int index;
    float tl, attenuation, dist;
    float3 lightDir, h;
    float4 colour = float4(0, 0, 0, 1), lambertian, blinnPhong;
    Light light;
    Mat mat;
    Ray shadowRay;
    Intersection shadowIntersection = GenerateIntersection();
    
    //create ray
    float2 sample = float2(id.z % samples, (int) (id.z * sampleInv)) + 1;
    sample = sample * nextSampleInv - 0.5;
    sample = mul(ROTATE26, sample);
    sample += id.xy + 0.5;
    
    //keep sample within current pixel
    sample.x = max(min(id.x + 1, sample.x), id.x);
    sample.y = max(min(id.y + 1, sample.y), id.y);

    float su = left + (right - left) * sample.x * resolution.z;
    float sv = bottom + (top - bottom) * sample.y * resolution.w;
    float3 dir = normalize(su * u + sv * v - d * camDirection);

    Ray ray = GenerateRay(camPosition, dir);
    Intersection intersection = GenerateIntersection();
    intersection = CheckIntersections(ray, intersection);
    
    if (intersection.t < INF) {
        mat = mats[intersection.matId];
        for (int i = 0; i < lightCount; i++) {
            light = lights[i];
            if (light.directional)
                lightDir = -light.direction; 
            else
                lightDir = normalize(light.position - intersection.position);
             
            //check if point is in shadow
            //if not, lambertian, phong, attenuation, etc
            shadowRay.Set(intersection.position + EPSILON * -lightDir, lightDir);
            shadowIntersection.Reset();
            shadowIntersection = CheckIntersections(shadowRay, shadowIntersection);
            tl = (light.position.x - shadowRay.origin.x) * shadowRay.invDirection.x;
            if (shadowIntersection.t <= tl)
                continue;
            
            lambertian = light.intensity * light.lightColour * mat.diffuseColour * max(0, dot(intersection.normal, lightDir));

            //bisector of direction to light and direction to cam
            h = normalize(-dir + lightDir);
            
            blinnPhong = light.intensity * light.lightColour * mat.specularColour * pow(max(0, dot(intersection.normal, h)), mat.hardness);

            //only calculate distance if both the lights linear and quadratic attenuation values are not 0
            if (!light.directional && (light.attenuation.x > 0 || light.attenuation.y > 0))
                dist = distance(light.position, intersection.position);
            else
                dist = 0;
            attenuation = 1 + light.attenuation.x * dist * dist + light.attenuation.y * dist + light.attenuation.z;
            attenuation = 1 / attenuation;
            //should clamp attenuation, so light sorces dont light to infinity
            attenuation = (attenuation - EPSILON) / (1 - EPSILON);
            attenuation = max(attenuation, 0);
            
            colour += (lambertian + blinnPhong) * attenuation;
        }

        colour += ambientIntensity * ambientColour;
    }
    
    colour *= sampleSquareInv;
    index = id.y * resolution.x * sampleSquare + id.x * sampleSquare;
    
    AAResults[index + id.z] = colour;
}

[numthreads(32, 32, 1)]
void CorrectImage (uint3 id : SV_DispatchThreadID) {
    if (id.x >= (uint) resolution.x || id.y >= (uint) resolution.y) return;
    int i = id.y * resolution.x * sampleSquare + id.x * sampleSquare;
    for (int z = 0; z < sampleSquare; z++)
        result[id.xy] += AAResults[i + z];
}

[numthreads(1, 1, 64)]
void AAReduction (uint3 id : SV_DispatchThreadID) {
    if (id.x >= (uint) resolution.x || id.y >= (uint) resolution.y || id.z > (uint) (sampleSquare * 0.5)) return;
    int i = id.y * resolution.x * sampleSquare + id.x * sampleSquare + id.z;
    
    float next = (int) (sampleSquare * 0.5);
    
    while ((uint) next > id.z) {
        AAResults[i] += AAResults[i + next];
        next = next * 0.5;
        GroupMemoryBarrierWithGroupSync();
    }
    
    if (id.z == 0)
        result[id.xy] = AAResults[i];
}

Intersection CheckForIntersections (RWStructuredBuffer<Mesh> objs, int count, Intersection intersection, Ray ray) {
    for (int i = 0; i < count; i++) {
        Mesh mesh = objs[i];
        
        //TODO
        
        //might be worthwhile to precompute normal of each triangle ahead of time. otherwise need to do a normalize + cross product for each triangle
        
        /*
        
        Check intersection with mesh's bounding box. continue if no intersection

        for each triangle in the mesh
            check if ray intersects triangle (using tri norm)
            calculate t (continue if bigger than previous known t)
            check backface culling (using tri norm) (continue if back collision)
            interpolate the normal at the collision position (using vert norms) to get the actual surface normal
            set new intersection


        */
    }
    
    return intersection;
}