#pragma kernel TraceRays
#pragma kernel CorrectImage
#pragma kernel AAReduction

#include "Includes/Geometry.cginc" //already includes constants and structs

//global variables that do not change
float4 ambientColour;
float ambientIntensity;

RWTexture2D<float4> result; //raytrace result
float4 resolution; //.x and .y are the resolution, .z and .w are the correspondeg inverses
float samples, sampleInv, nextSampleInv, sampleSquare, sampleSquareInv;
RWStructuredBuffer<float4> AAResults;

//d is (i think) focal length. near/far planes not considered yet
float3 camDirection; //direction TO cam, not direction cam is facing
float3 camPosition;

//variables for generating rays
float d;
float top;
float right;
float bottom;
float left;
float3 u;
float3 v;

RWStructuredBuffer<Light> lights;
int lightCount;
RWStructuredBuffer<Mat> mats;
int matCount;

//scene objects
RWStructuredBuffer<Sphere> spheres;
int sphereCount;

RWStructuredBuffer<Plane> planes;
int planeCount;

RWStructuredBuffer<Quadric> quadrics;
int quadricCount;

RWStructuredBuffer<AABB> boxes;
int AABBCount;

Intersection CheckIntersections (Ray ray) {
    Intersection intersection = GenerateIntersection();
    
    intersection = CheckForIntersections(spheres, sphereCount, intersection, ray);
    intersection = CheckForIntersections(planes, planeCount, intersection, ray);
    intersection = CheckForIntersections(quadrics, quadricCount, intersection, ray);
    return CheckForIntersections(boxes, AABBCount, intersection, ray);
}

//Note, product must be <= 1024
[numthreads(16, 16, 4)]
void TraceRays (uint3 id : SV_DispatchThreadID) {
    if (id.x >= (uint) resolution.x || id.y >= (uint) resolution.y || id.z >= (uint) sampleSquare) return;
    
    float2 sample = float2(id.z % samples, (int) (id.z * sampleInv)) + 1;
    sample = sample * nextSampleInv - 0.5;
    sample = mul(ROTATE26, sample);
    sample += id.xy + 0.5;
    
    //keep sample within current pixel
    sample.x = max(min(id.x + 1, sample.x), id.x);
    sample.y = max(min(id.y + 1, sample.y), id.y);

    float su = left + (right - left) * sample.x * resolution.z;
    float sv = bottom + (top - bottom) * sample.y * resolution.w;
    float3 dir = normalize(su * u + sv * v - d * camDirection);

    Ray ray = GenerateRay(camPosition, dir);
    Intersection intersection = CheckIntersections(ray);
    
    float4 colour = float4(0, 0, 0, 1);
    if (intersection.t < INF) {
        Mat mat = mats[intersection.matId];
        for (int i = 0; i < lightCount; i++) {
            Light light = lights[i];
            float3 lightDir;
            if (light.directional)
                lightDir = -light.direction; else
                lightDir = normalize(light.position - intersection.position);
             
            //check if point is in shadow
            //if not, lambertian, phong, attenuation, etc
            Ray shadowRay = GenerateRay(intersection.position + EPSILON * -lightDir, lightDir);
            Intersection shadowIntersection = CheckIntersections(shadowRay);
            float tl = (light.position.x - shadowRay.origin.x) * shadowRay.invDirection.x;
            if (shadowIntersection.t <= tl)
                continue;
            
            float4 lambertian = light.intensity * light.lightColour * mat.diffuseColour * max(0, dot(intersection.normal, lightDir));

            float3 h = normalize(-dir + lightDir);
            float4 bp = light.intensity * light.lightColour * mat.specularColour * pow(max(0, dot(intersection.normal, h)), mat.hardness);

            colour += lambertian + bp;
        }

        colour += ambientIntensity * ambientColour;
    }
    
    colour *= sampleSquareInv;
    int i = id.y * resolution.x * sampleSquare + id.x * sampleSquare;
    
    AAResults[i + id.z] = colour;
}

[numthreads(32, 32, 1)]
void CorrectImage (uint3 id : SV_DispatchThreadID) {
    if (id.x >= (uint) resolution.x || id.y >= (uint) resolution.y) return;
    int i = id.y * resolution.x * sampleSquare + id.x * sampleSquare;
    for (int z = 0; z < sampleSquare; z++)
        result[id.xy] += AAResults[i + z];
}

[numthreads(1, 1, 64)]
void AAReduction (uint3 id : SV_DispatchThreadID) {
    if (id.x >= (uint) resolution.x || id.y >= (uint) resolution.y || id.z > (uint) (sampleSquare * 0.5)) return;
    int i = id.y * resolution.x * sampleSquare + id.x * sampleSquare + id.z;
    
    float next = (int) (sampleSquare * 0.5);
    
    while ((uint) next > id.z) {
        AAResults[i] += AAResults[i + next];
        next = next * 0.5;
        GroupMemoryBarrierWithGroupSync();
    }
    
    if (id.z == 0)
        result[id.xy] = AAResults[i];
}